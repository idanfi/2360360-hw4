%{
#include <string>
#include "hw5_output.hpp"
#include "Types.h"
#include "SymbolTable.h"
#include "SemanticCheck.h"
#include "bp.hpp"
#include "registerAllocator.h"

using std::string;
using namespace output;
using namespace SemanticCheck;

CodeBuffer& buffer = CodeBuffer::instance();
extern RegisterAllocator regAllocator;
extern int yylineno;
extern char *yytext;
int yylex();
void yyerror(const char*);
%}

%token VOID
%right INT
%token BYTE
%token BOOL
%token TRUE
%token FALSE
%token RETURN
%token WHILE
%token BREAK
%token CONTINUE
%token SWITCH
%token CASE
%token DEFAULT
%token COLON
%token SC
%token COMMA
%token LBRACE RBRACE
%right ASSIGN
%left OR
%left AND
%left EQUALITY
%nonassoc RELATIONAL
%left PLUS MINUS
%left MUL DEV
%right NOT
%right LPAREN RPAREN
%token ID
%token NUM
%token B
%token STRING
%right IF
%left ELSE

%%
Program: Funcs {isMainMissing(); symbolTable.deleteScope();}
;

Funcs:  /*epsilon*/ {if (*yytext != 0) {errorSyn(yylineno); exit(-1);} }
        | FuncDecl Funcs {}
;
FuncDecl: RetType ID LPAREN Formals RPAREN LBRACE {symbolTable.addFunction($2->id, $1->type, (FormalDeclList*)$4);} Statements RBRACE {symbolTable.endFunctionHandler();}

;
RetType: Type {$$ = $1;}

;
RetType: VOID {$$ = $1;}

;
Formals: /* epsilon */ {$$ = new FormalDeclList();}
         | FormalsList {$$ = $1;}

;
FormalsList: FormalDecl {$$ = new FormalDeclList($1);}
             | FormalDecl COMMA FormalsList {((FormalDeclList *)$3)->addFormalDecl($1); $$ = $3;}

;
FormalDecl: Type ID {$$ = new Node($1->type, $2->id);}

;
Statements: Statement {$$ = $1;}
            | Statements Statement {$$->mergeLists($1, $2);}

;
Statement: LBRACE BlockOpen Statements RBRACE {symbolTable.deleteScope(); $$ = $3;}
           | Type ID SC            {symbolTable.addVariable($2->id, $1->type); regAllocator.createRegister($2, "0", INVALID_ID); $$ = $1;}
           | Type ID ASSIGN Exp SC {symbolTable.addVariable($2->id, $1->type); $4->loadExp(); regAllocator.createRegister($2, $4->value, $4->id); $$ = new AssignOp($2, $4);}
           | ID ASSIGN Exp SC      {symbolTable.assertExists($1->id, false); $3->loadExp(); regAllocator.createRegister($1, $3->value, $4->id); $$ = new AssignOp($1, $3);}
           | Call SC               {$$ = $1;}
           | RETURN SC             {isValidReturn(TYPE_VOID, false); $$ = new TypeExp(TYPE_VOID); buffer.emit("ret void");}
           | RETURN Exp SC         {isValidReturn($2->realtype(), true); $2->loadExp(); $2->emitReturnCode(); $$ = $2;}
           | IF IfOpenMarker LPAREN IfExp RPAREN Statement {symbolTable.deleteScope(); $$ = $4;}
           | IF IfOpenMarker LPAREN IfExp RPAREN Statement ELSE {symbolTable.deleteScope(); symbolTable.addConditionalStructure("else");} Statement {symbolTable.deleteScope(); $$ = $4;}
           | WHILE WhileOpen LPAREN WhileExp RPAREN Statement {symbolTable.removeWhile(); $6->emitWhileEnd($2->nextInstruction, $4); $$ = $6;}
           | BREAK SC                                      {isInLoop(false); $$ = $1; $$->addBreak();}
           | CONTINUE SC                                   {isInLoop(true);  $$ = $1; $$->addContinue();}
           | SWITCH SwitchOpen LPAREN SwitchExp RPAREN LBRACE CaseList RBRACE {symbolTable.removeSwitch(); ((CaseList *)$7)->emitCase($4->nextList, $4->value);}
;

BlockOpen: /*epsilon*/ {symbolTable.createScope();}
;

IfOpenMarker: /*epsilon*/ {symbolTable.addConditionalStructure("if");}
;

IfExp: Exp {assertBoolean($1); $1->loadExp(); $$ = $1;}
;

WhileOpen: /*epsilon*/ {symbolTable.addConditionalStructure("while"); $$ = new Node(); $$->emitWhileOpen();}
;

WhileExp: Exp {assertBoolean($1); $1->loadExp(); $$->emitWhileExp($1->value); $$ = $1;}
;

SwitchOpen: /*epsilon*/ {symbolTable.addConditionalStructure("switch"); $$ = new Node();}
;

SwitchExp: Exp {assertAssignableTypes(TYPE_INT, "dummy", $1->realtype(), "dummy"); $1->loadExp(); $$ = $1; $$->emitSwitchOpen();}
;

Call:   ID LPAREN ExpList RPAREN {$1->emitCallCode($3); $$ = new CallOp($1, (ExpList*)$3);}
        | ID LPAREN RPAREN {$1->emitCallCode(nullptr); $$ = new CallOp($1, new ExpList());}
;

ExpList:    Exp {$1->loadExp(); $$ = new ExpList($1);}
            | Exp COMMA ExpList {$1->loadExp(); ((ExpList *)$3)->addExp($1); $$ = $3;}
;

Type:   INT {$$ = $1;}
        | BYTE {$$ = $1;}
        | BOOL {$$ = $1;}
;

Exp:    LPAREN Exp RPAREN {$2->loadExp(); $$ = $2;}
        | Exp MINUS Exp {buffer.emit(regAllocator.createArithmeticCode($1, $3, "sub")); $$ = new BinaryArithmeticOp($1, $3); $$->value = regAllocator.getCurrentRegisterName();}
        | Exp PLUS Exp {buffer.emit(regAllocator.createArithmeticCode($1, $3, "add")); $$ = new BinaryArithmeticOp($1, $3);$$->value = regAllocator.getCurrentRegisterName();}
        | Exp DEV Exp {buffer.emit(regAllocator.createArithmeticCode($1, $3, "sdiv")); $$ = new BinaryArithmeticOp($1, $3); $$->value = regAllocator.getCurrentRegisterName();}
        | Exp MUL Exp {buffer.emit(regAllocator.createArithmeticCode($1, $3, "mul")); $$ = new BinaryArithmeticOp($1, $3); $$->value = regAllocator.getCurrentRegisterName();}
        | ID {$$ = $1; symbolTable.assertExists($1->id, false); $$->value = regAllocator.getVarRegister($1->id);}
        | Call {$$ = $1;}
        | NUM {$$ = new NumberExp($1);}
        | NUM B {$$ = new NumberExp($1, true);}
        | STRING {$$ = $1; }
        | TRUE {$$ = $1; $$->value = "1"; $$->trueList.push_back({buffer.nextInstruction(), FIRST});}
        | FALSE {$$ = $1; $$->value = "0"; $$->falseList.push_back({buffer.nextInstruction(), SECOND});}
        | NOT Exp {$$ = new UnaryLogicOp($2);}
        | Exp AND BoolMarkerM Exp {$$ = new BinaryLogicOp($1, $4, true, $3);}
        | Exp OR  BoolMarkerM Exp {$$ = new BinaryLogicOp($1, $4, false, $3);}
        | Exp EQUALITY Exp {$$ = new RelOp($1, $3, $2->id);}
        | Exp RELATIONAL Exp {$$ = new RelOp($1, $3, $2->id);}
        //| LPAREN Type RPAREN Exp {}
;

BoolMarkerM: /* epsilon */ {$$ = new Marker(true);}
;

BoolMarkerN: /* epsilon */ {$$ = new Marker(false);}
;

CaseList:   CaseDecl CaseList {$$ = $2; ((CaseList *)$$)->addCase($1->id, $1->nextInstruction); $$->mergeLists($1, $2);}
            | CaseDecl {$$ = $1;}
            | DEFAULT DefaultLabel COLON Statements {$$ = new CaseList(); ((CaseList *)$$)->hasDefault = true; ((CaseList *)$$)->defaultLabel = $2->nextInstruction; $$->nextList = $4->nextList;}
;

DefaultLabel: /* epsilon */ {$$ = new Node(); $$->emitCaseLabel();}

CaseDecl:   CASE CaseHead NUM COLON Statements {$$ = new CaseList($3->id, $2->nextInstruction); $$->nextList = $5->nextList;}
;

CaseHead: /* epsilon */ { $$ = new Node(); $$->emitCaseLabel();}

%%
void yyerror(const char*) {errorSyn(yylineno); exit(-1);}
int main() {
    buffer.preprocess();
    yyparse();
    buffer.printGlobalBuffer();
    buffer.printCodeBuffer();
    return 0;
}
